apply from: 'gradle/helper.gradle'

tasks.register("installAngular", NpmTask) {
    group = "Angular"
    description = "Installs Angular"
    dependsOn "nodeSetup"
    args = ["install", "-g", "@angular/cli@${angularVersion}"]
}

def ngCreate = tasks.register("createAngularApp", NodeTask) {
    group = "Angular"
    description = "Creates a new angular app"
    def appDir = getAppDir()
    def applicationName = getAppName()
    script = findNgScript()
    args = ["new", "--skip-git", "--routing", "--style=scss", "--directory=${appDir}", applicationName]
}

def disableAnalytics = tasks.register("disableAnalytics", NodeTask) {
    group = "Angular"
    description = "Disables Angular Analytics"
    script = findNgScript()
    def appDir = getAppDir()
    workingDir = file("./" + appDir)
    args = ["analytics", "disable"]
}

tasks.register("setupAngularApp") {
    group = "Angular"
    description = "Creates a new Angular App and disables analytics"
    dependsOn ngCreate
    dependsOn disableAnalytics
    tasks.findByName("disableAnalytics").mustRunAfter(ngCreate)
}


private File findNgScript() {
    println("Finding ng...")
    def nodeSubDir = new File("${project.rootDir}/.gradle/nodejs")
            .listFiles()
    def nodeDir = nodeSubDir.find { it.name.contains("node-v${nodeVersion}") }
    return file("${nodeDir}/lib/node_modules/@angular/cli/bin/ng.js")
}

